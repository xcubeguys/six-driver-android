apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.purpletalk"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            buildConfigField "String", "SERVER_URL", '"http://"'
            buildConfigField "boolean", "FABRIC_ENABLED", 'false'
            buildConfigField "boolean", "PRINT_LOG", 'true'
        }

        stage {
            buildConfigField "String", "SERVER_URL", '"http://"'
            buildConfigField "boolean", "FABRIC_ENABLED", 'true'
            buildConfigField "boolean", "PRINT_LOG", 'true'
        }

        playstore {
            buildConfigField "String", "SERVER_URL", '"https://"'
            buildConfigField "boolean", "FABRIC_ENABLED", 'true'
            buildConfigField "boolean", "PRINT_LOG", 'false'
        }
    }

    // noinspection GroovyAssignabilityCheck
    task checkstyle(type: Checkstyle) {
        //NOTE: Restrict to this project related code only and ignore other third party code.
        source 'src/main/java/SixTnC'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'

        ignoreFailures false
        showViolations true

        configFile file("${project.rootDir}/config/checkstyle.xml")

        classpath = files() + configurations.compile
    }
    // inspection GroovyAssignabilityCheck

    preBuild.dependsOn('checkstyle')

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    //Recycler view
    compile 'com.android.support:recyclerview-v7:25.3.1'

    //https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.9'
    //https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    //https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-gson
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    //https://mvnrepository.com/artifact/com.squareup.retrofit2/adapter-rxjava
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'

    testCompile 'junit:junit:4.12'
}
